#
# Defines environment variables before .zshrc is sourced
#

# Dotfiles
# ----------------------------------------------------------------------------
export DOTFILES=~/.dotfiles

# Editors
# ----------------------------------------------------------------------------
export EDITOR='vim'
export VISUAL='vim'

# Language
# ----------------------------------------------------------------------------
[[ -z "$LANG" ]] && eval "$(locale)"

# Cache and temp files
# ----------------------------------------------------------------------------
export CACHEDIR="$HOME/.cache"
[[ -d "$CACHEDIR" ]] || mkdir -p "$CACHEDIR"

export ZCACHEDIR="$CACHEDIR/zsh"
[[ -d "$ZCACHEDIR" ]] || mkdir -p "$ZCACHEDIR"

if [[ -d "$TMPDIR" ]]; then
	export TMPPREFIX="${TMPDIR%/}/zsh"
	[[ -d "$TMPPREFIX" ]] || mkdir -p "$TMPPREFIX"
fi

# General paths
# ----------------------------------------------------------------------------
typeset -gU cdpath fpath mailpath manpath path
typeset -gUT INFOPATH infopath

# Set the the list of directories that cd searches.
cdpath=($cdpath)

# Set the list of directories that man searches for manuals.
manpath=(
	/usr/local/share/man
	/usr/share/man
	$manpath
)
for path_file in /etc/manpaths.d/*(.N); do
	manpath+=($(<$path_file))
done
unset path_file

# Set the list of directories that Zsh searches for programs.
path=(
	./node_modules/.bin
	$HOME/Applications
	$HOME/Dropbox/Bin
	$HOME/bin
	$DOTFILES/bin
	/usr/local/{bin,sbin}
	$path
)
for path_file in /etc/paths.d/*(.N); do
	path+=($(<$path_file))
done
unset path_file

fpath=($DOTFILES/zsh/functions $fpath)

# Java
# ----------------------------------------------------------------------------
JAVA_HOME=`/usr/libexec/java_home 2> /dev/null`
if (( $? )); then
	unset JAVA_HOME
else
	export JAVA_HOME
fi

# Python
# ----------------------------------------------------------------------------
if (( $+commands[virtualenv] )); then
	VIRTUAL_ENV_PROJECT_ENV_NAME="venv"
	VIRTUAL_ENV_DISABLE_PROMPT=1
fi

[[ -e ~/Library/Python/2.7/bin ]] && path+=~/Library/Python/2.7/bin

# Go
# ----------------------------------------------------------------------------
if (( $+commands[go] )); then
	GOROOT=`go env GOROOT 2> /dev/null`
	[[ -n "$GOROOT" ]] && path+=$GOROOT/bin
	case "$OSTYPE" in
		darwin*) export GOPATH=$HOME/Documents/Programming/go ;;
		linux*)  export GOPATH=$HOME/go ;;
	esac
	path+=$GOPATH/bin
fi

# Ruby
# ----------------------------------------------------------------------------
if [[ -d $HOME/Library/Ruby/Gems/2.0.0 ]]; then
	export GEM_HOME=$HOME/Library/Ruby/Gems/2.0.0
	path+=$GEM_HOME/bin
fi

# Node
# ----------------------------------------------------------------------------
nvmsh="$(brew --prefix nvm)/nvm.sh"
if [[ -e "$nvmsh" ]]; then
	export NVM_DIR=~/.nvm
	source $nvmsh
fi
unset nvmsh

# Android
# ------------------------------------------------------------------------
if (( $+commands[android] )); then
	export ANDROID_HOME=`echo $(which android)(:A:h:h)`
fi

# Local config
# --------------------------------------------------------------------------
[[ -f ~/.config/zsh/zshenv ]] && source ~/.config/zsh/zshenv

# vim:shiftwidth=4:tabstop=4:noexpandtab
