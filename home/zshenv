#
# Defines environment variables before .zshrc is sourced
#
# This file should be kept light and fast. Anything that's slow or involves
# user interaction should go in zshrc.

# Dotfiles
# ----------------------------------------------------------------------------
export DOTFILES=$HOME/.dotfiles

# Language
# ----------------------------------------------------------------------------
[[ -z "$LANG" ]] && eval "$(locale)"
[[ -z "$LANG" ]] && export LANG=en_US.UTF-8
[[ -z "$LC_ALL" ]] && export LC_ALL=$LANG

# Cache and temp files
# ----------------------------------------------------------------------------
export CACHEDIR="$HOME/.local/share"
[[ -d "$CACHEDIR" ]] || mkdir -p "$CACHEDIR"

export ZCACHEDIR="$CACHEDIR/zsh"
[[ -d "$ZCACHEDIR" ]] || mkdir -p "$ZCACHEDIR"

export ZFUNCDIR="$CACHEDIR/zsh/functions"
[[ -d "$ZFUNCDIR" ]] || mkdir -p "$ZFUNCDIR"

export ZCOMPDIR="$CACHEDIR/zsh/completions"
[[ -d "$ZCOMPDIR" ]] || mkdir -p "$ZCOMPDIR"

if [[ -d "$TMPDIR" ]]; then
	export TMPPREFIX="${TMPDIR%/}/zsh"
	[[ -d "$TMPPREFIX" ]] || mkdir -p "$TMPPREFIX"
fi

# General paths
# ----------------------------------------------------------------------------
typeset -gU cdpath fpath mailpath manpath path
typeset -gUT INFOPATH infopath

# Make all path entries unique
typeset -aU path

# Move /usr/local/bin top the top
path=(
	$HOME/Applications
	$DOTFILES/bin
	/usr/local/bin
	${path:#/usr/local/bin}
)


# Set the list of directories that Zsh searches for function scripts.
fpath=(
    $ZFUNCDIR
    $DOTFILES/zsh/functions
    $fpath
)


# Set the list of directories that man searches for manuals.
if [ -e /etc/manpaths ]; then
	while read line; do 
		manpath+=$line
	done < /etc/manpaths
fi

if [ -x /usr/libexec/path_helper ]; then
	eval `/usr/libexec/path_helper -s`
fi

# Java
# ----------------------------------------------------------------------------
if [[ -z "$JAVA_HOME" ]]; then
	if [[ -x /usr/libexec/java_home ]]; then
		export JAVA_HOME=`/usr/libexec/java_home -v 12 2> /dev/null`
	fi
fi

# Ruby
# ----------------------------------------------------------------------------
if [[ -d /usr/local/opt/ruby ]]; then
	path=(
		/usr/local/opt/ruby/bin
		$path
	)
fi
if [[ -d /usr/local/lib/ruby/gems/2.6.0 ]]; then
	path=(
		/usr/local/lib/ruby/gems/2.6.0/bin
		$path
	)
fi

# Rust
# ----------------------------------------------------------------------------
if [[ -d $HOME/.cargo/bin ]]; then
	path=(
		$HOME/.cargo/bin
		$path
	)
fi

# Python
# ----------------------------------------------------------------------------
if [[ -d /usr/local/opt/python3 ]]; then
	# Put python at the end of the path since some of its programs, like pip,
	# will eventually be overridden with things in /usr/local/bin
	# https://discourse.brew.sh/t/pip-install-upgrade-pip-breaks-pip-when-installed-with-homebrew/5338
	path=(
		$path
		/usr/local/opt/python3/libexec/bin
	)
fi

# Go
# ----------------------------------------------------------------------------
if (( $+commands[go] )); then
	case "$OSTYPE" in
		darwin*) export GOPATH=$HOME/Documents/Programming/go ;;
		linux*)  export GOPATH=$HOME/go ;;
	esac
	path=(
		$GOPATH/bin
		$path
	)
fi

# Android
# ------------------------------------------------------------------------
if (( $+commands[android] )); then
	export ANDROID_HOME=`echo $(which android)(:A:h:h)`
fi

# PHP
# ------------------------------------------------------------------------
path=(
	$HOME/.composer/vendor/bin
	$path
	/usr/local/opt/php@7.1/bin
	/usr/local/opt/php@7.1/sbin
)


# TeX
# ------------------------------------------------------------------------
if [[ -e /usr/local/texlive ]]; then 
	path=(
		$path
		/usr/local/texlive/2018/bin/x86_64-darwin
	)
fi

# Terminal
# --------------------------------------------------------------------------
if [[ -z $TERM_PROGRAM ]]; then
	if [[ -n $GNOME_TERMINAL_SCREEN ]]; then
		export TERM_PROGRAM=gnome-terminal
	elif [[ -n $KITTY_LISTEN_ON ]]; then
		export TERM_PROGRAM=kitty
	fi
fi

# Local config
# --------------------------------------------------------------------------
[[ -f $HOME/.config/zsh/zshenv ]] && source $HOME/.config/zsh/zshenv

# Disable global zprofile, which messes with the path
unsetopt GLOBAL_RCS

# vim:shiftwidth=4:tabstop=4:noexpandtab
