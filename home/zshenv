#
# Defines environment variables before .zshrc is sourced
#

# DOTFILES ###################################################################

export DOTFILES=~/.dotfiles

# Editors ####################################################################

export EDITOR='vim'
export VISUAL='vim'

# Language ###################################################################

[[ -z "$LANG" ]] && eval "$(locale)"

# Less #######################################################################

export LESS='-F -g -i -M -R -w -X -z-4'
export PAGER='less'

# Cache and temp files #######################################################

export CACHEDIR="$HOME/.cache"
[[ -d "$CACHEDIR" ]] || mkdir -p "$CACHEDIR"

export ZCACHEDIR="$CACHEDIR/zsh"
[[ -d "$ZCACHEDIR" ]] || mkdir -p "$ZCACHEDIR"

if [[ -d "$TMPDIR" ]]; then
	export TMPPREFIX="${TMPDIR%/}/zsh"
	[[ -d "$TMPPREFIX" ]] || mkdir -p "$TMPPREFIX"
fi

# General paths ##############################################################

typeset -gU cdpath fpath mailpath manpath path
typeset -gUT INFOPATH infopath

# Set the the list of directories that cd searches.
cdpath=($cdpath)

# Set the list of directories that man searches for manuals.
manpath=(
	/usr/local/share/man
	/usr/share/man
	$manpath
)
for path_file in /etc/manpaths.d/*(.N); manpath+=($(<$path_file))
unset path_file

# Set the list of directories that Zsh searches for programs.
path=(
	./node_modules/.bin
	$HOME/Applications
	$HOME/Dropbox/Bin
	$HOME/bin
	/usr/local/{bin,sbin}
	$path
)
for path_file in /etc/paths.d/*(.N); path+=($(<$path_file))
unset path_file

fpath=($DOTFILES/zsh/functions $fpath)

# terminfo ###################################################################

# Tell terminfo to use both the home directory and /usr/share/terminfo to avoid
# tput missing terminal definition errors.
[[ -e ~/.terminfo ]] && export TERMINFO_DIRS=~/.terminfo:/usr/share/terminfo

# Java #######################################################################

[[ -e /usr/libexec/java_home ]] && export JAVA_HOME=`/usr/libexec/java_home`

# Python #####################################################################

if [[ $+commands[virtualenv] ]]; then
	VIRTUAL_ENV_PROJECT_ENV_NAME="venv"
	VIRTUAL_ENV_DISABLE_PROMPT=1
fi

[[ -e ~/Library/Python/2.7/bin ]] && export PATH=$PATH:~/Library/Python/2.7/bin

# Go #########################################################################

if (( $+commands[go] )); then
	GOROOT=`go env GOROOT 2> /dev/null`
	[[ -n "$GOROOT" ]] && path+=$GOROOT/bin
	case "$OSTYPE" in
		darwin*) export GOPATH=$HOME/Documents/Programming/go ;;
		linux*)  export GOPATH=$HOME/go ;;
	esac
	path+=$GOPATH/bin
fi

# Ruby #######################################################################

case "$OSTYPE" in
	darwin*) export GEM_HOME=$HOME/Library/Ruby/Gems/2.0.0 ;;
	linux*)  export GEM_HOME=$HOME/.gem/ruby/1.9.1 ;;
esac
path+=$GEM_HOME/bin

# Android ################################################################

export ANDROID_HOME=`echo $(which android)(:A:h:h)`

# Local config #############################################################

[[ -f ~/.config/zsh/zshenv ]] && source ~/.config/zsh/zshenv

# vim:shiftwidth=4:tabstop=4:noexpandtab
