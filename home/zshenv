#
# Defines environment variables before .zshrc is sourced
#
# This file should be kept light and fast. Anything that's slow or involves
# user interaction should go in zshrc.
#

# Dotfiles
# ----------------------------------------------------------------------------
export DOTFILES=~/.dotfiles

# Editors
# ----------------------------------------------------------------------------
export EDITOR='vi'
export VISUAL='vi'

# Language
# ----------------------------------------------------------------------------
[[ -z "$LANG" ]] && eval "$(locale)"
[[ -z "$LLANG" ]] && export LANG=en_US.UTF-8

# Cache and temp files
# ----------------------------------------------------------------------------
export CACHEDIR="$HOME/.local/share"
[[ -d "$CACHEDIR" ]] || mkdir -p "$CACHEDIR"

export ZCACHEDIR="$CACHEDIR/zsh"
[[ -d "$ZCACHEDIR" ]] || mkdir -p "$ZCACHEDIR"

if [[ -d "$TMPDIR" ]]; then
	export TMPPREFIX="${TMPDIR%/}/zsh"
	[[ -d "$TMPPREFIX" ]] || mkdir -p "$TMPPREFIX"
fi

# General paths
# ----------------------------------------------------------------------------
typeset -gU cdpath fpath mailpath manpath path
typeset -gUT INFOPATH infopath

# Set the list of directories that man searches for manuals.
while read line; do 
	manpath+=$line
done < /etc/manpaths

if [[ -e ~/Library/Python/2.7/bin ]]; then 
	path=(~/Library/Python/2.7/bin $path)
fi

if [ -x /usr/libexec/path_helper ]; then
	eval `/usr/libexec/path_helper -s`
fi

# Set the list of directories that Zsh searches for function scripts.
fpath=($DOTFILES/zsh/functions /usr/local/share/zsh-completions $fpath)

# Java
# ----------------------------------------------------------------------------
if [[ -z "$JAVA_HOME" ]]; then
	JAVA_HOME=`/usr/libexec/java_home 2> /dev/null`
	if (( $? )); then
		unset JAVA_HOME
	else
		export JAVA_HOME
	fi
fi

# Go
# ----------------------------------------------------------------------------
if (( $+commands[go] )); then
	case "$OSTYPE" in
		darwin*) export GOPATH=$HOME/Documents/Programming/go ;;
		linux*)  export GOPATH=$HOME/go ;;
	esac
	path=(
		$GOPATH/bin
		$path
	)
fi

# Ruby
# ----------------------------------------------------------------------------
if [[ -d $HOME/Library/Ruby/Gems/2.0.0 ]]; then
	export GEM_HOME=$HOME/Library/Ruby/Gems/2.0.0
	path=(
		$GEM_HOME/bin
		$path
	)
fi

# Android
# ------------------------------------------------------------------------
if (( $+commands[android] )); then
	export ANDROID_HOME=`echo $(which android)(:A:h:h)`
fi

# Path
# ------------------------------------------------------------------------
path=(
	$HOME/Applications
	$DOTFILES/bin
	$path
)

# Local config
# --------------------------------------------------------------------------
[[ -f ~/.config/zsh/zshenv ]] && source ~/.config/zsh/zshenv

# Disable global zprofile, which messes with the path
unsetopt GLOBAL_RCS

# vim:shiftwidth=4:tabstop=4:noexpandtab
