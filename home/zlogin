#
# Executes commands at login post-zshrc
#

# Execute code that does not affect the current session in the background
{
	# Compile the completion dump to increase startup speed
	if [[ "$ZCOMPFILE" -nt "${ZCOMPFILE}.zwc" || ! -s "${ZCOMPFILE}.zwc" ]]; then
		zcompile "$ZCOMPFILE"
	fi

	# Set environment variables for launchd processes
	if [[ "$OSTYPE" == darwin* ]]; then
		if [[ -n "$TMUX" ]]; then
			# If we're running in tmux, we need to wrap the launchctl calls
			# with reattach-to-user-namespace
			function _launchctl {
				reattach-to-user-namespace launchctl $@
			}
		else
			function _launchctl {
				launchctl $@
			}
		fi
		for env_var in PATH MANPATH; do
			_launchctl setenv "$env_var" "${(P)env_var}"
		done
	fi
} &!
