PROFILE_STARTUP=false
if [[ "$PROFILE_STARTUP" == true ]]; then
    # http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html
    PS4=$'%D{%M%S%.} %N:%i> '
    exec 3>&2 2>$HOME/Temp/startlog.$$
    setopt xtrace prompt_subst
fi

# Editors
# ----------------------------------------------------------------------------
if (( $+commands[nvim] )); then
	export SUDO_EDITOR=nvim
elif (( $+commands[vim] )); then
	export SUDO_EDITOR=vim
else
	export SUDO_EDITOR=vi
fi

export EDITOR=$SUDO_EDITOR
export VISUAL=$SUDO_VISUAL

# Autoload all user shell functions, following symlinks
# ------------------------------------------------------------------------
if [[ -d $DOTFILES/zsh/functions ]]; then
    for func in $DOTFILES/zsh/functions/*(:t); autoload -U $func
fi

# Create an array to hold plugin references
# ------------------------------------------------------------------------
typeset -A plugins

# Start SSH agent if it's not already running
# ------------------------------------------------------------------------
if [[ -z $SSH_AUTH_SOCK || ! -a $SSH_AUTH_SOCK ]]; then
    if [[ -e $HOME/.ssh/env ]]; then
        . $HOME/.ssh/env
    fi
    if [[ ! -a $SSH_AUTH_SOCK ]]; then
        eval `ssh-agent` > /dev/null
        echo "export SSH_AUTH_SOCK=$SSH_AUTH_SOCK" >! $HOME/.ssh/env
    fi
fi

# Turn on color for everything
# ------------------------------------------------------------------------
zstyle ':zconf:*:*' color 'yes'
autoload -Uz colors && colors

# Terminal colors
TC='\e['
Rst="${TC}0m"
Blk="${TC}30m";
Red="${TC}31m";
Grn="${TC}32m";
Yel="${TC}33m";
Blu="${TC}34m";
Prp="${TC}35m";
Cyn="${TC}36m";
Wht="${TC}37m";

# Completions
# ------------------------------------------------------------------------
# Docker completions
if [[ -d /Applications/Docker.app/Contents/Resources/etc ]]; then
    for f in /Applications/Docker.app/Contents/Resources/etc/*.zsh-completion; do
        dest=$ZCOMPDIR/_$(basename -s .zsh-completion $f)
        if [[ ! -f $dest || $f -nt $dest ]]; then
            cp $f $dest
            chmod a-x $dest
        fi
    done
    unset dest
fi

# Extra ZSH completions
zfetch $ZPLUGDIR zsh-users/zsh-completions
if [[ $? == 0 ]]; then
    fpath=($ZPLUGDIR/zsh-users/zsh-completions/src $fpath)
fi

# Load and initialize the completion system, ignoring insecure directories
export ZCOMPFILE=$CACHEDIR/zsh/zcompdump
autoload -Uz compinit && compinit -i -d $ZCOMPFILE

# Use caching to make completion for commands with many options usable
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path "${ZCACHEDIR}"

# Case-insensitive (all), partial-word, and then substring completion
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'

# Group matches and describe
zstyle ':completion:*'              menu select
zstyle ':completion:*:matches'      group 'yes'
zstyle ':completion:*:options'      description 'yes'
zstyle ':completion:*:options'      auto-description '%d'
zstyle ':completion:*:corrections'  format ' %F{green}-- %d (errors: %e) --%f'
zstyle ':completion:*:descriptions' format ' %F{yellow}-- %d --%f'
zstyle ':completion:*:messages'     format ' %F{purple} -- %d --%f'
zstyle ':completion:*:warnings'     format ' %F{red}-- no matches found --%f'
zstyle ':completion:*:default'      list-prompt '%S%M matches%s'
zstyle ':completion:*'              format ' %F{yellow}-- %d --%f'
zstyle ':completion:*'              group-name ''
zstyle ':completion:*'              verbose yes

# Fuzzy match mistyped completions
zstyle ':completion:*'               completer _complete _match _approximate
zstyle ':completion:*:match:*'       original only
zstyle ':completion:*:approximate:*' max-errors 1 numeric

# Increase the number of errors based on the length of the typed word
zstyle -e ':completion:*:approximate:*' max-errors 'reply=($((($#PREFIX+$#SUFFIX)/3))numeric)'

# Don't complete unavailable commands
zstyle ':completion:*:functions' ignored-patterns '(_*|pre(cmd|exec))'

# Array completion element sorting
zstyle ':completion:*:*:-subscript-:*' tag-order indexes parameters

# Directories
zstyle ':completion:*:default'                list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*:*:cd:*'                 tag-order local-directories directory-stack path-directories
zstyle ':completion:*:*:cd:*:directory-stack' menu yes select
zstyle ':completion:*:-tilde-:*'              group-order 'named-directories' 'path-directories' 'users' 'expand'
zstyle ':completion:*'                        squeeze-slashes true

# History
zstyle ':completion:*:history-words' stop yes
zstyle ':completion:*:history-words' remove-all-dups yes
zstyle ':completion:*:history-words' list false
zstyle ':completion:*:history-words' menu yes

# Environmental Variables
zstyle ':completion::*:(-command-|export):*' fake-parameters ${${${_comps[(I)-value-*]#*,}%%,*}:#-*-}

# Populate hostname completion
zstyle -e ':completion:*:hosts' hosts 'reply=(
    ${=${=${=${${(f)"$(cat {/etc/ssh_,~/.ssh/known_}hosts(|2)(N) 2>/dev/null)"}%%[#| ]*}//\]:[0-9]*/ }//,/ }//\[/ }
    ${=${(f)"$(cat /etc/hosts(|)(N) <<(ypcat hosts 2>/dev/null))"}%%\#*}
    ${=${${${${(@M)${(f)"$(cat ~/.ssh/config 2>/dev/null)"}:#Host *}#Host }:#*\**}:#*\?*}}
)'

# Don't complete uninteresting users...
zstyle ':completion:*:*:*:users' ignored-patterns \
    adm amanda apache avahi beaglidx bin cacti canna clamav daemon \
    dbus distcache dovecot fax ftp games gdm gkrellmd gopher \
    hacluster haldaemon halt hsqldb ident junkbust ldap lp mail \
    mailman mailnull mldonkey mysql nagios \
    named netdump news nfsnobody nobody nscd ntp nut nx openvpn \
    operator pcap postfix postgres privoxy pulse pvm quagga radvd \
    rpc rpcuser rpm shutdown squid sshd sync uucp vcsa xfs '_*'

# ... unless we really want to
zstyle '*' single-ignored show

# Ignore multiple entries
zstyle ':completion:*:(rm|kill|diff):*' ignore-line other
zstyle ':completion:*:rm:*' file-patterns '*:all-files'

# kill
zstyle ':completion:*:*:*:*:processes'    command 'ps -u $USER -o pid,user,comm -w'
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#) ([0-9a-z-]#)*=01;36=0=01'
zstyle ':completion:*:*:kill:*'           menu yes select
zstyle ':completion:*:*:kill:*'           force-list always
zstyle ':completion:*:*:kill:*'           insert-ids single

# man
zstyle ':completion:*:manuals'       separate-sections true
zstyle ':completion:*:manuals.(^1*)' insert-sections true

# SSH/SCP/RSYNC
zstyle ':completion:*:(scp|rsync):*'                  tag-order 'hosts:-host:host hosts:-domain:domain hosts:-ipaddr:ip\ address *'
zstyle ':completion:*:(scp|rsync):*'                  group-order users files all-files hosts-domain hosts-host hosts-ipaddr
zstyle ':completion:*:ssh:*'                          tag-order 'hosts:-host:host hosts:-domain:domain hosts:-ipaddr:ip\ address *'
zstyle ':completion:*:ssh:*'                          group-order users hosts-domain hosts-host users hosts-ipaddr
zstyle ':completion:*:(ssh|scp|rsync):*:hosts-host'   ignored-patterns '*(.|:)*' loopback ip6-loopback localhost ip6-localhost broadcasthost
zstyle ':completion:*:(ssh|scp|rsync):*:hosts-domain' ignored-patterns '<->.<->.<->.<->' '^[-[:alnum:]]##(.[-[:alnum:]]##)##' '*@*'
zstyle ':completion:*:(ssh|scp|rsync):*:hosts-ipaddr' ignored-patterns '^(<->.<->.<->.<->|(|::)([[:xdigit:].]##:(#c,2))##(|%*))' '127.0.0.<->' '255.255.255.255' '::1' 'fe80::*'

# Key bindings
# ------------------------------------------------------------------------
# Define friendly names for keycodes
typeset -gA key_info
key_info=(
    'Control'      '\C-'
    'ControlLeft'  '\e[1;5D \e[5D \e\e[D \eOd'
    'ControlRight' '\e[1;5C \e[5C \e\e[C \eOc'
    'Escape'       '\e'
    'Meta'         '\M-'
    'Backspace'    "^?"
    'Delete'       "^[[3~"
    'F1'           "$terminfo[kf1]"
    'F2'           "$terminfo[kf2]"
    'F3'           "$terminfo[kf3]"
    'F4'           "$terminfo[kf4]"
    'F5'           "$terminfo[kf5]"
    'F6'           "$terminfo[kf6]"
    'F7'           "$terminfo[kf7]"
    'F8'           "$terminfo[kf8]"
    'F9'           "$terminfo[kf9]"
    'F10'          "$terminfo[kf10]"
    'F11'          "$terminfo[kf11]"
    'F12'          "$terminfo[kf12]"
    'Insert'       "$terminfo[kich1]"
    'Home'         "$terminfo[khome]"
    'PageUp'       "$terminfo[kpp]"
    'End'          "$terminfo[kend]"
    'PageDown'     "$terminfo[knp]"
    'Up'           "$terminfo[kcuu1]"
    'Left'         "$terminfo[kcub1]"
    'Down'         "$terminfo[kcud1]"
    'Right'        "$terminfo[kcuf1]"
    'BackTab'      "$terminfo[kcbt]"
)

# VI keybindings
bindkey -v

# Aliases
# ------------------------------------------------------------------------
# Clear existing aliases
unalias -m '*'

# Default command options
alias cp='cp -i'
alias ln='ln -i'
alias mkdir='mkdir -p'
alias mv='mv -i'
alias rm='rm -i'
alias type='type -a'

# Let node use local readline setup (vi mode)
if (( $+commands[rlwrap] )); then
	alias node='env NODE_NO_READLINE=1 rlwrap node'
fi

# Shortcuts
alias agl='ag -l'
alias rgl='rg -l'
alias back='popd'
alias help='run-help'
alias se='sudo -e'

# Disable certificate check for wget
alias wget='wget --no-check-certificate'

# Docker shortcuts
alias dk='docker kill'
alias dl='docker logs'
alias dlf='docker logs -f'
alias dps='docker ps --format "{{.Names}}"'

# Themes
alias mtd='mac_theme dark'
alias mtl='mac_theme light'

# Git shortcuts
alias ga='git add'
alias gb='git branch'
alias gba='git branch -a'
alias gbd='git branch -d'
alias gbf='git branch --contains'
alias gc='git commit'
alias gca='git commit --all'
alias gcb='git checkout -b'
alias gco='git checkout'
alias gcp='git cherry-pick --ff'
alias gcpn='git cherry-pick --no-commit'
alias gd='git diff'
alias gdh='git diff HEAD'
alias gdp='git diffprev'
alias gds='git diff --stat'
alias gdv='git difftool'
alias gf='git fetch'
alias gfp='git fetch --prune'
alias gfr='git fetchreset'
alias gfpr='git pr'
alias gg='git grep'
alias ggi='git grep --ignore-case'
alias ggil='git grep --ignore-case --files-with-matches'
alias ggl='git grep --files-with-matches'
alias gid='git rev-parse HEAD'
alias gkb='git killbranch'
alias gl='git lg -n 20'
alias gla='git lg'
alias gl1='git log -1'
alias glb='git lgb -n 20'
alias glba='git lgb'
alias glbn='git lgb -n'
alias glf='git log-from'
alias glfm='git log-from-merge'
alias glg='git log --topo-order --pretty=format:${_git_log_medium_format}'
alias gln='git lg -n'
alias gls='git ls-files'
alias gmb='git merge-base'
alias gp='git pullthis'
alias gr='git rebase'
alias gri='git rebase -i'
alias grc='git rebase --continue'
alias grm='git rm'
alias gro='git rebase --onto'
alias grv='git remote -v'
alias gs='git s'
alias gsc='git diff --name-only --diff-filter=U'
alias gsh='git show'
alias gss='git show --stat'
alias gwa='git worktree add'
alias gwl='git worktree list'
alias gwp='git worktree prune'
alias tiga='tig --all'

# tmux
alias tls='tmux list-sessions'
alias tas='tmux attach -t'
alias tks='tmux kill-session -t'

# Pretty print json
alias json='python -m json.tool'

# Tree
alias t1='tree -L 1'

# Disable correction for some commands
alias cd="nocorrect ${aliases[cd]:-cd}"
alias cp="nocorrect ${aliases[cp]:-cp}"
alias gcc="nocorrect ${aliases[gcc]:-gcc}"
alias grep="nocorrect ${aliases[grep]:-grep}"
alias gulp="nocorrect ${aliases[gulp]:-gulp}"
alias ln="nocorrect ${aliases[ln]:-ln}"
alias man="nocorrect ${aliases[man]:-man}"
alias mkdir="nocorrect ${aliases[mkdir]:-mkdir}"
alias mv="nocorrect ${aliases[mv]:-mv}"
alias rm="nocorrect ${aliases[rm]:-rm}"
alias vim="nocorrect ${aliases[vim]:-vim}"
alias tsd="nocorrect ${aliases[tsd]:-tsd}"
alias jake="nocorrect ${aliases[jake]:-jake}"

# Disable globbing for some commands
alias bower="noglob ${aliases[bower]:-bower}"
alias find="noglob ${aliases[find]:-find}"
alias ftp="noglob ${aliases[ftp]:-ftp}"
alias history="noglob ${aliases[history]:-history}"
alias rsync="noglob ${aliases[rsync]:-rsync}"
alias scp="noglob ${aliases[scp]:-scp}"
alias sftp="noglob ${aliases[sftp]:-sftp}"

# Default swift compilation options
alias swiftc="xcrun -sdk macosx swiftc"

alias npmv="npm --loglevel error"
alias npmr="npm --registry=https://registry.npmjs.org"

# Colorize ls
if zstyle -t ':zconf:module:utility:ls' color; then
    # Define colors for BSD ls
    export LSCOLORS='exfxcxdxbxGxDxabagacad'

    # Define colors for the completion system
    export LS_COLORS='di=34:ln=35:so=32:pi=33:ex=31:bd=36;01:cd=33;01:su=31;40;07:sg=36;40;07:tw=32;40;07:ow=33;40;07:'

    alias ls='ls -G -F'
else
    alias ls='ls -F'
fi

# Other ls aliases
alias l='ls -1A'         # Lists in one column, hidden files
alias ll='ls -lh'        # Lists human readable sizes
alias la='ll -A'         # Lists human readable sizes, hidden files

# Space usage
alias df='df -kh'
alias du='du -kh'

# Shell options
# ------------------------------------------------------------------------
# Navigation
setopt   AUTO_CD                # Change to a directory without typing cd
setopt   AUTO_NAME_DIRS         # Auto add variable-stored paths to ~ list
setopt   AUTO_PUSHD             # Push the old directory onto the stack on cd
setopt   CDABLE_VARS            # Change directory to a path stored in a variable
setopt   EXTENDED_GLOB          # Use extended globbing syntax
setopt   MULTIOS                # Write to multiple descriptors
setopt   PUSHD_IGNORE_DUPS      # Do not store duplicates in the stack
setopt   PUSHD_SILENT           # Do not print the directory stack after pushd or popd
setopt   PUSHD_TO_HOME          # Push to home directory when no argument is given
unsetopt CLOBBER                # Don't overwrite existing files with > and >>

# Files and commands
setopt   BRACE_CCL              # Allow brace character class list expansion
setopt   COMBINING_CHARS        # Combine zero-length punctuation characters (accents) with the base character
setopt   CORRECT                # Correct mis-typed commands
setopt   RC_QUOTES              # Allow 'Henry''s Garage' instead of 'Henry'\''s Garage'
unsetopt MAIL_WARNING           # Don't print a warning message if a mail file has been accessed

# Jobs
setopt   AUTO_RESUME            # Attempt to resume existing job before creating a new process
setopt   LONG_LIST_JOBS         # List jobs in the long format by default
setopt   NOTIFY                 # Report status of background jobs immediately
unsetopt BG_NICE                # Don't run all background jobs at a lower priority
unsetopt CHECK_JOBS             # Don't report on jobs when shell exit
unsetopt HUP                    # Don't kill jobs on shell exit

# History
setopt   BANG_HIST              # Treat the '!' character specially during expansion
setopt   HIST_BEEP              # Beep when accessing non-existent history
setopt   HIST_EXPIRE_DUPS_FIRST # Expire a duplicate event first when trimming history
setopt   HIST_FIND_NO_DUPS      # Do not display a previously found event
setopt   HIST_IGNORE_ALL_DUPS   # Delete an old recorded event if a new event is a duplicate
setopt   HIST_IGNORE_DUPS       # Do not record an event that was just recorded again
setopt   HIST_IGNORE_SPACE      # Do not record an event starting with a space
setopt   HIST_SAVE_NO_DUPS      # Do not write a duplicate event to the history file
setopt   HIST_VERIFY            # Do not execute immediately upon history expansion
setopt   SHARE_HISTORY          # Share history between all sessions

# Completion
setopt   ALWAYS_TO_END          # Move cursor to the end of a completed word
setopt   AUTO_LIST              # Automatically list choices on ambiguous completion
setopt   AUTO_PARAM_SLASH       # If completed parameter is a directory, add a trailing slash
setopt   COMPLETE_IN_WORD       # Complete from both ends of a word
setopt   PATH_DIRS              # Perform path search even on command names with slashes
unsetopt CASE_GLOB              # Make globbing case insensitive
unsetopt FLOW_CONTROL           # Disable start/stop characters in shell editor

# History
# ------------------------------------------------------------------------
HISTFILE="$ZCACHEDIR/zhistory"  # The path to the history file
HISTSIZE=10000                # The maximum number of events to save in the internal history
SAVEHIST=10000                # The maximum number of events to save in the history file

# Lists the ten most used commands
alias history-stat="history 0 | awk '{print \$2}' | sort | uniq -c | sort -n -r | head"

# Less
# ----------------------------------------------------------------------------
export LESS='-F -g -i -M -R -w -X -z-4'
export PAGER='less'

# fzf (https://github.com/junegunn/fzf)
# ------------------------------------------------------------------------
if (( $+commands[fzf] )); then
    export FZF_PATH=`echo $(which fzf)(:A:h:h)`

    # Make FZF respond more quickly when hitting escape
    # https://github.com/junegunn/fzf.vim/issues/248
    export ESCDELAY=10

    # Use -1 for the bg color to specify 'none'
    export FZF_DEFAULT_OPTS=--color=16,bg:-1,fg:11,bg+:10,spinner:14,info:14,fg+:12,hl+:8,hl:8

	if [[ -d $FZF_PATH/shell ]]; then
		source $FZF_PATH/shell/completion.zsh
		source $FZF_PATH/shell/key-bindings.zsh
	elif [[ -d /usr/share/doc/fzf ]]; then
		source /usr/share/doc/fzf/examples/key-bindings.zsh
	fi

    bindkey '^P' fzf-file-widget
    bindkey '^G' fzf-cd-widget

    if (( $+commands[rg] )); then
        export FZF_DEFAULT_COMMAND='rg --files --no-ignore --hidden --follow --glob "!.git/*"'
    elif (( $+commands[ag] )); then
        export FZF_DEFAULT_COMMAND='ag -g ""'
    fi

    # Use rg instead of the default find command for listing candidates.
    # - The first argument to the function is the base path to start traversal
    # - Note that ag only lists files not directories
    # - See the source code (completion.{bash,zsh}) for the details.
    _fzf_compgen_path() {
        rg -g "" "$1"
    }
fi

# direnv (https://github.com/direnv/direnv)
# ------------------------------------------------------------------------
if (( $+commands[direnv] )); then
    eval "$(direnv hook $SHELL)"
fi

# Line editor
# ------------------------------------------------------------------------
# Syntax highlighting -- this should be near the end (preferably at the end) of the zshrc
# https://github.com/zsh-users/zsh-syntax-highlighting
zfetch $ZPLUGDIR zdharma/fast-syntax-highlighting
if [[ $? == 0 ]]; then
    source $ZPLUGDIR/zdharma/fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh
    if [[ -n $ZSH_HIGHLIGHT_VERSION ]]; then
        ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets pattern cursor)
    fi
fi

# History substring search -- load this after zsh-syntax-highlighting
# https://github.com/zsh-users/zsh-history-substring-search
zfetch $ZPLUGDIR zsh-users/zsh-history-substring-search
if [[ $? == 0 ]]; then
    source $ZPLUGDIR/zsh-users/zsh-history-substring-search/zsh-history-substring-search.zsh

    # Color for found substrings
	HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_FOUND='bg=14,fg=0,bold'

    # Color for not-found substrings
    HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_NOT_FOUND='bg=1,fg=0,bold'

    # Default globbing flags
    HISTORY_SUBSTRING_SEARCH_GLOBBING_FLAGS='i'

    # Check if case-sensitive is in the substring search style
    if zstyle -t ':zconf:module:history-substring-search' case-sensitive; then
      HISTORY_SUBSTRING_SEARCH_GLOBBING_FLAGS="${HISTORY_SUBSTRING_SEARCH_GLOBBING_FLAGS//i}"
    fi

    # Check if color is disabled
    if ! zstyle -t ':zconf:module:history-substring-search' color; then
      unset HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_{FOUND,NOT_FOUND}
    fi

    # Make history keybindings search
    bindkey -M vicmd "k" history-substring-search-up
    bindkey -M vicmd "j" history-substring-search-down
fi

# autosuggest -- load this after zsh-syntax-highlighting and zsh-history-substring-search
# (https://github.com/tarruda/zsh-autosuggestions)
zfetch $ZPLUGDIR zsh-users/zsh-autosuggestions
if [[ $? == 0 ]]; then
    source $ZPLUGDIR/zsh-users/zsh-autosuggestions/zsh-autosuggestions.zsh

    # Let vi keys jump through the suggestion
    bindkey '^f' vi-forward-word
    bindkey '^b' vi-forward-blank-word
    bindkey '^e' vi-end-of-line

    # Use a solarized-friendly background color
    export ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=14'

    export ZSH_AUTOSUGGEST_USE_ASYNC=1

    # Configure autosuggest to work properly with history substring search;
    # without this, trying to history-substring-search with an empty line will
    # hang zsh
    ZSH_AUTOSUGGEST_CLEAR_WIDGETS=("${(@)ZSH_AUTOSUGGEST_CLEAR_WIDGETS:#(up|down)-line-or-history}")
    ZSH_AUTOSUGGEST_CLEAR_WIDGETS+=(history-substring-search-up history-substring-search-down)
fi

# History key bindings
# ------------------------------------------------------------------------
# Up and down arrow keys step through local history
up-line-or-local-history() {
    zle set-local-history 1
    zle up-line-or-history
    zle set-local-history 0
}
zle -N up-line-or-local-history
down-line-or-local-history() {
    zle set-local-history 1
    zle down-line-or-history
    zle set-local-history 0
}
zle -N down-line-or-local-history
bindkey '^[[A' up-line-or-local-history
bindkey '^[[B' down-line-or-local-history

# Setup a precmd to let Terminal.app know the CWD
if [[ "$TERM_PROGRAM" == "Apple_Terminal" ]]; then
    update_cwd () {
        local SEARCH=' '
        local REPLACE='%20'
        local PWD_URL="file://$HOSTNAME${PWD//$SEARCH/$REPLACE}"
        printf '\e]7;%s\a' "$PWD_URL"
    }
    [[ -z $precmd_functions ]] && precmd_functions=()
    precmd_functions=($precmd_functions update_cwd)

    if [[ "$TERM" == "xterm-256color" ]]; then
        export TERM=xterm-256color-italic
    fi
fi

# Node
# ----------------------------------------------------------------------------
if (( $+commands[nodenv] )); then
    eval "$(nodenv init -)"
fi

# Load NPX fallback script
if (( $+commands[npx] )); then
    npx_fallback="$DOTFILES/zsh/npx.zsh"
    source "$npx_fallback"
fi

# Load NPM completion
if (( $+commands[npm] )); then
    npm_completer="$DOTFILES/zsh/npm.zsh"
    source "$npm_completer"

	zfetch $ZPLUGDIR lukechilds/zsh-better-npm-completion
	if [[ $? == 0 ]]; then
		source $ZPLUGDIR/lukechilds/zsh-better-npm-completion/zsh-better-npm-completion.plugin.zsh
	fi
fi

# TPM
# ------------------------------------------------------------------------
if [[ ! -d $CACHEDIR/tmux ]]; then
    mkdir $CACHEDIR/tmux
fi
zfetch $CACHEDIR/tmux tmux-plugins/tpm

# Prompt
# ------------------------------------------------------------------------
zfetch $ZPLUGDIR jason0x43/pure
if [[ $? == 0 ]]; then
	if [[ \
		-h $ZFUNCDIR/prompt_pure_setup \
		&& $(link_target $ZFUNCDIR/prompt_pure_setup) != $ZPLUGDIR/jason0x43/pure/pure.zsh \
	]]; then
        rm -f $ZFUNCDIR/prompt_pure_setup
        rm -f $ZFUNCDIR/async
	fi
    if [[ ! -h $ZFUNCDIR/prompt_pure_setup ]]; then
        ln -s $ZPLUGDIR/jason0x43/pure/pure.zsh $ZFUNCDIR/prompt_pure_setup
        ln -s $ZPLUGDIR/jason0x43/pure/async.zsh $ZFUNCDIR/async
    fi
fi
autoload -Uz promptinit && promptinit
prompt pure

# Local config
# --------------------------------------------------------------------------
[[ -f ~/.config/zsh/zshrc ]] && source ~/.config/zsh/zshrc

# added by travis gem
[ -f /Users/jason/.travis/travis.sh ] && source /Users/jason/.travis/travis.sh

if [[ "$PROFILE_STARTUP" == true ]]; then
    unsetopt xtrace
    exec 2>&3 3>&-
fi

# vim:shiftwidth=4:tabstop=4:noexpandtab
