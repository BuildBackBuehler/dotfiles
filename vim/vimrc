" let g:js_indent_logging = 1

" Put all swap and undo files in a central location
if exists('$CACHEDIR')
	set directory=$CACHEDIR/vim/swap
	set undodir=$CACHEDIR/vim/undo
	set backupdir=$CACHEDIR/vim/backup
endif

" Set the leader character
let mapleader=';'

set clipboard+=unnamed   " Yank to the system clipboard
set cmdheight=2          " Keep vim from saying 'Press Enter to continue'
set undofile             " Use undo files for persistent undo
set fillchars+=vert:\â”‚   " Make the vertical split bar solid
set hidden               " Use hidden buffers
set ignorecase           " Ignore case when searching...
set smartcase            " ...unless the search is mixed case
set nojoinspaces         " Only insert 1 space after a period when joining lines
set nostartofline        " Don't move to the beginning of a line when jumping
set number               " Show line numbers
set completeopt-=preview " Don't show the preview window when completing		     
set scrolloff=5          " Give the cursor a 5 line margin when scrolling
set shiftwidth=4         " Indent in 4-character wide chunks
set showcmd              " Show commands in the cmd area
set tabstop=4            " Tabs are only 4 characters wide
set formatoptions+=ron   " Automatically format comments and numbered lists
set ruler				 " Show location info in statusline
set nowrap				 " Don't wrap text by default
set wildignorecase       " Case insensitive filename completion
" set shortmess+=A		 " Use brief message when a swap file exists

" Toggle cursor crosshairs
map <silent> # :set cursorcolumn! cursorline!<CR>

" Show markers at the beginning and end of non-wrapped lines
set listchars+=precedes:^,extends:$

" Ignore binary files in the standard vim file finder
set wildignore+=*.pyc,*.obj,*.bin,a.out

" <Leader>w to write
map <silent> <Leader>w :w<CR>
map <silent> <Leader>W :w!<CR>

" <Leader>q to quit
map <silent> <Leader>q :qall<CR>
map <silent> <Leader>Q :qall!<CR>

" <Leader>c to close a window
map <silent> <Leader>c :close<CR>

" <Leader>k to kill a buffer (Bd comes from vim-bbye)
map <silent> <Leader>k :Bd<CR>
map <silent> <Leader>K :bd!<CR>

" Show the syntax status of the character under the cursor
map <silent> <Leader>h :echo "hi<" . 
	\ synIDattr(synID(line("."),col("."),1),"name") 
	\ . '> trans<'
	\ . synIDattr(synID(line("."),col("."),0),"name") . "> lo<"
	\ . synIDattr(synIDtrans(synID(line("."),col("."),1)),"name") . ">"<CR>

" Files and buffers
map <Leader>f :find 
map <silent> <Leader>b :ls<CR> 

" Ag for grepping (https://github.com/ggreer/the_silver_searcher)
if executable('ag')
	set grepprg=ag\ --nogroup\ --nocolor
endif

" Better start/end atching
runtime! macros/matchit.vim

" Easier navigation of text files
function! TextMode()
	setlocal wrap linebreak nolist display+=lastline
	map <buffer> <silent> k gk
	map <buffer> <silent> j gj
	map <buffer> <silent> 0 g0
	map <buffer> <silent> $ g$
endfunction

" General autocommands
augroup vimrc
	autocmd!

	" Make text files easier to work with
	autocmd FileType text call TextMode()
	autocmd FileType textile call TextMode()
	autocmd FileType markdown call TextMode()
	autocmd FileType html call TextMode()

	" Always start at the top of git commit messages
	autocmd FileType gitcommit call setpos('.', [0, 1, 1, 0])

	" Close help files on 'q'
	autocmd FileType help nnoremap q :bd<CR>

	autocmd BufEnter * syntax sync fromstart

	" Automatically close preview windows after autocompletion
	autocmd CompleteDone * pclose

	" Identify files in a zsh/functions directory as zsh scripts
	autocmd BufRead */zsh/functions/* set filetype=zsh

	" When editing a file, jump back to the last edited line if it makes sense
	" to do so
	autocmd BufReadPost *
		\ if line("'\"") > 1 && line("'\"") <= line("$") |
		\     execute "normal! g'\"" |
		\ endif
augroup END

" EasyAlign
" ---------------------------------------------------------------------
" To align end-of-line comments, do <Leader>a-<space>
map <silent> <Leader>a <Plug>(EasyAlign)

" Flagship
" ---------------------------------------------------------------------
" Don't show the tabline when using Flagship
let g:tablabel=''

" fzf
" ---------------------------------------------------------------------
let g:fzf_layout = { 'down': 10 }

" Go
" ---------------------------------------------------------------------
let g:go_fmt_command = "goimports"
let g:go_fmt_fail_silently = 1

" JavaScript
" ---------------------------------------------------------------------
let g:html_indent_script1 = 'inc'
let g:html_indent_style1 = 'inc'
let g:js_indent_flat_switch = 1

" json
" ---------------------------------------------------------------------
" Don't conceal quotes around JSON properties
let g:vim_json_syntax_conceal = 0

" NERDTree
" ---------------------------------------------------------------------
let g:NERDTreeAutoDeleteBuffer=1
let g:NERDTreeMinimalUI=1
let g:NERDTreeDirArrows=1
let g:NERDTreeWinSize=40
map <silent> <Leader>n :NERDTreeFind<CR>

" Neomake
" ---------------------------------------------------------------------
" Better highlighting for signs
let g:neomake_warning_sign = { 'texthl': 'WarningSign' }
let g:neomake_error_sign = { 'texthl': 'ErrorSign' }
let g:neomake_list_height = 5

augroup neomake_write
	autocmd!
	autocmd BufWritePost * Neomake
augroup END

" Neovim
" ---------------------------------------------------------------------
if has('nvim')
	" <Leader>e to escape into command mode in an nvim terminal
	tnoremap <Leader>e <C-\><C-n>

	" Tell Neovim.app which Python to use
	if exists('/usr/local/bin/python')
		let g:python_host_prog='/usr/local/bin/python'
	endif
	if exists('/usr/local/bin/python3')
		let g:python3_host_prog='/usr/local/bin/python3'
	endif
endif

" netrw
" ---------------------------------------------------------------------
" Use tree-style listings by default
let g:netrw_liststyle = 3

" Supertab
" ---------------------------------------------------------------------
let g:SuperTabDefaultCompletionType = 'context'

" UltiSnips
" --------------------------------------------------------------------- 
augroup ultisnips
	function! OnCompleteDone()
		if !exists('v:completed_item')
			return
		endif
		if has_key(v:completed_item, 'menu') && v:completed_item['menu'] =~ '^<snip> '
			" Completed item was a snippet -- expand it
			call UltiSnips#ExpandSnippet()
		else
			if s:completed_with_enter
				" User pressed enter to end completion, but selection wasn't a
				" snippet, so send an Enter keypress
				execute "normal i \<CR>"
			endif
		endif

		" Always clear the completed_with_enter flag
		let s:completed_with_enter = 0
	endfunction

	autocmd!
	autocmd CompleteDone * call OnCompleteDone()
augroup END

" When Enter is pressed and the completion popup is visible, set a flag
" indicating that completion was ended and send a C-Y key to perform
" completion.
let s:completed_with_enter = 0
function! CompletedWithEnter()
	let s:completed_with_enter = 1
	return "\<C-Y>"
endfunction
inoremap <expr> <CR> pumvisible() ? CompletedWithEnter() : "\<CR>"

" Set C-J as a backup for snippet expansion
let g:UltiSnipsExpandTrigger = "<C-J>"

" undotree
" --------------------------------------------------------------------- 
nnoremap <Leader>u :UndotreeToggle<CR>

" Plugins
" ---------------------------------------------------------------------
call plug#begin('~/.vim/plugged')

Plug 'tpope/vim-commentary'           " gc for commenting code blocks
Plug 'tpope/vim-eunuch'               " POSIX command wrappers
Plug 'tpope/vim-flagship'             " Status line configuration
Plug 'tpope/vim-fugitive'             " Git utilities
Plug 'tpope/vim-unimpaired'           " gc for commenting code blocks
Plug 'moll/vim-bbye'                  " Preserve layout when closing buffers
Plug 'christoomey/vim-tmux-navigator' " Easy movement between vim and tmux panes
Plug 'benekastah/neomake'             " Asynchronous make and error highlighter
Plug 'junegunn/vim-easy-align'        " Easy vertical alignment of code elements
Plug 'ervandew/supertab'              " Use tab for autocompletion
Plug 'mbbill/undotree'                " Visualize the undo tree
Plug 'keith/swift.vim'                " Swift

" Completion
Plug 'Valloric/YouCompleteMe'

" File browsing
Plug 'scrooloose/nerdtree', { 'on': [ 'NERDTreeToggle', 'NERDTreeFind' ] }
Plug 'Xuyuanp/nerdtree-git-plugin', { 'on': [ 'NERDTreeToggle', 'NERDTreeFind' ] }

" Snippet management
Plug 'SirVer/ultisnips' | Plug 'honza/vim-snippets'

" Session management
Plug 'tpope/vim-obsession'

" Filetype plugins
Plug 'digitaltoad/vim-jade', { 'for': 'jade' }
Plug 'leafgarland/typescript-vim', { 'for': 'typescript' }
Plug 'clausreinke/typescript-tools.vim', { 'for': 'typescript' }
Plug 'applescript.vim', { 'for': 'applescript' }
Plug 'Textile-for-VIM', { 'for': 'textile' }
Plug 'dag/vim-fish', { 'for': 'fish' }
Plug 'elzr/vim-json', { 'for': 'json' }
Plug 'groenewege/vim-less', { 'for': 'less' }
Plug 'rust-lang/rust.vim'

Plug 'jason0x43/vim-js-indent', { 'for':  [ 'javascript', 'typescript', 'html', 'jsp' ] }
Plug 'jason0x43/vim-js-syntax', { 'for':  [ 'javascript', 'html' ] }
Plug 'mxw/vim-jsx', {'for': 'javascript.jsx'} 

Plug 'tpope/vim-markdown', { 'for': 'markdown' }
Plug 'tpope/vim-classpath', { 'for': 'java' }
Plug 'wavded/vim-stylus', { 'for': 'stylus' }

" Load the go plugin if go is available
if executable('go')
	Plug 'fatih/vim-go', { 'for': 'go' }
endif

" Load the fzf plugin if fzf is available
if executable('fzf') && has('nvim') && !has('gui_running')
	Plug $FZF_PATH
	Plug 'junegunn/fzf.vim'

	" These mappings depend on the FZF plugin being loaded
	map <silent> <Leader>f :Files<CR>
	map <silent> <Leader>b :Buffers<CR>
	map <silent> <Leader>g :GFiles<CR>
	map <silent> <Leader>s :Snippets<CR>
endif

" Load all the plugins
call plug#end()

" Turn on all the filetype goodness
" ---------------------------------------------------------------------
filetype plugin indent on
syn on
colorscheme solarized
